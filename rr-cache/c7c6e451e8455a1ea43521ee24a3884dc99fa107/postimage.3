// Copyright (C) 2016 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply from: '../../constants.gradle'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'androidx.media3.demo.main'

    compileSdkVersion project.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        versionName project.ext.releaseVersion
        versionCode project.ext.releaseVersionCode
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.appTargetSdkVersion
        multiDexEnabled true
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles = [
                "proguard-rules.txt",
                getDefaultProguardFile('proguard-android.txt')
            ]
            signingConfig signingConfigs.debug
        }
        debug {
            jniDebuggable = true
        }
    }

    lintOptions {
        // The demo app isn't indexed, doesn't have translations, and has a
        // banner for AndroidTV that's only in xhdpi density.
        disable 'GoogleAppIndexingWarning','MissingTranslation','IconDensities'
    }

    flavorDimensions "decoderExtensions"

    productFlavors {
        noDecoderExtensions {
            dimension "decoderExtensions"
            buildConfigField "boolean", "USE_DECODER_EXTENSIONS", "false"
        }
        withDecoderExtensions {
            dimension "decoderExtensions"
            buildConfigField "boolean", "USE_DECODER_EXTENSIONS", "true"
        }
    }
}

// Version of AndroidX Media3 to pull from Google Maven (defaults to current release)
project.ext.baseAndroidxMediaVersion = project.ext.releaseVersion

dependencies {
    compileOnly 'org.checkerframework:checker-qual:' + checkerframeworkVersion
    implementation 'androidx.annotation:annotation:' + androidxAnnotationVersion
    implementation 'androidx.appcompat:appcompat:' + androidxAppCompatVersion
    implementation 'androidx.multidex:multidex:' + androidxMultidexVersion
    implementation 'com.google.android.material:material:' + androidxMaterialVersion
    implementation project(modulePrefix + 'lib-exoplayer')
    implementation ('androidx.media3:media3-exoplayer-dash:' + baseAndroidxMediaVersion) {
        exclude group: 'androidx.media3', module: 'media3-exoplayer'
        exclude group: 'androidx.media3', module: 'media3-extractor'
    }
    implementation project(modulePrefix + 'lib-exoplayer-hls')
    implementation ('androidx.media3:media3-exoplayer-rtsp:' + baseAndroidxMediaVersion) {
        exclude group: 'androidx.media3', module: 'media3-exoplayer'
        exclude group: 'androidx.media3', module: 'media3-extractor'
    }
    implementation ('androidx.media3:media3-exoplayer-smoothstreaming:' + baseAndroidxMediaVersion) {
        exclude group: 'androidx.media3', module: 'media3-exoplayer'
        exclude group: 'androidx.media3', module: 'media3-extractor'
    }
    implementation ('androidx.media3:media3-ui:' + baseAndroidxMediaVersion) {
        exclude group: 'androidx.media3', module: 'media3-exoplayer'
        exclude group: 'androidx.media3', module: 'media3-extractor'
    }
    implementation ('androidx.media3:media3-datasource-cronet:' + baseAndroidxMediaVersion) {
        exclude group: 'androidx.media3', module: 'media3-extractor'
    }
    implementation ('androidx.media3:media3-exoplayer-ima:' + baseAndroidxMediaVersion) {
        exclude group: 'androidx.media3', module: 'media3-exoplayer'
        exclude group: 'androidx.media3', module: 'media3-extractor'
    }
    withDecoderExtensionsImplementation 'androidx.media3:media3-decoder-av1:' + baseAndroidxMediaVersion
    withDecoderExtensionsImplementation 'androidx.media3:media3-decoder-ffmpeg:' + baseAndroidxMediaVersion
    withDecoderExtensionsImplementation 'androidx.media3:media3-decoder-flac:' + baseAndroidxMediaVersion
    withDecoderExtensionsImplementation 'androidx.media3:media3-decoder-opus:' + baseAndroidxMediaVersion
    withDecoderExtensionsImplementation 'androidx.media3:media3-decoder-midi:' + baseAndroidxMediaVersion
    withDecoderExtensionsImplementation 'androidx.media3:media3-datasource-rtmp:' + baseAndroidxMediaVersion
}

apply plugin: 'com.google.android.gms.strict-version-matcher-plugin'
